package command;

class CommandRegistry {
	final commands:Array<Command> = [];

	public function new() {}

	public function add(name:String, perform:CommandContext->Void, ?predicate:CommandContext->Bool):Void {
		if (name.indexOf(":") <= 0 || name.indexOf(" ") >= 0)
			throw "invalid command name: " + name;
		for (index in 0...commands.length) {
			if (commands[index].name == name) {
				commands[index] = new Command(name, perform, predicate);
				return;
			}
		}
		commands.push(new Command(name, perform, predicate));
	}

	public function contains(name:String):Bool
		return find(name) != null;

	public function remove(name:String):Bool {
		var command = find(name);
		return command != null && commands.remove(command);
	}

	public function isValid(name:String, context:CommandContext):Bool {
		var command = find(name);
		if (command == null)
			return false;
		var predicate = command.predicate;
		return predicate(context);
	}

	public function perform(name:String, context:CommandContext):Bool {
		var command = find(name);
		if (command == null)
			return false;
		var predicate = command.predicate;
		if (!predicate(context))
			return false;
		var action = command.perform;
		action(context);
		return true;
	}

	function find(name:String):Null<Command> {
		for (command in commands)
			if (command.name == name)
				return command;
		return null;
	}
}
